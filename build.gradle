plugins {
    id 'org.springframework.boot' version '3.4.4' apply false
    id "org.sonarqube" version "6.3.1.5724"
    id 'jacoco'
}

allprojects {
    group = 'io.github.yyytir777'
    version = "1.2.0"
}

subprojects {
    if (project.name == "mocka-bom") return

    apply plugin: 'java'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy tasks.named("jacocoTestReport")
    }

    apply plugin: 'jacoco'
    jacoco { toolVersion = "0.8.11" }

    jacocoTestReport {
        reports {
            xml.required = true // xml 형태의 리포트 생성
            html.required = true
        }
    }

    test {
        useJUnitPlatform()
    }

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublishing {
            publishToMavenCentral()
            signAllPublications()

            pom {
                inceptionYear = "2025"
                url = "https://github.com/yyytir777/Mocka"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "yyytir777"
                        name = "Wonjae Lim"
                        url = "https://github.com/yyytir777"
                    }
                }

                scm {
                    url = "https://github.com/yyytir777/Mocka"
                    connection = "scm:git:git://github.com/yyytir777/Mocka.git"
                    developerConnection = "scm:git:ssh://git@github.com/yyytir777/Mocka.git"
                }
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.11"
}

sonar {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "yyytir777"
        property "sonar.projectKey", "yyytir777_mocka"
    }
}

tasks.register('aggregateJavadocs', Javadoc) {
    description = "Generates a combined Javadoc for all subprojects"
    group = "documentation"

    def javaProjects = subprojects.findAll { it.plugins.hasPlugin('java') }

    source javaProjects.collect {
        it.extensions.getByType(JavaPluginExtension)
                .sourceSets.named('main').get().allJava
    }

    classpath = files(javaProjects.collect {
        it.extensions.getByType(JavaPluginExtension)
                .sourceSets.named('main').get().compileClasspath
    })

    destinationDir = file("$buildDir/docs/javadoc")

    options.encoding = 'UTF-8'
    failOnError = false
}

tasks.register("printVersion") {
    doLast {
        println project.version
    }
}